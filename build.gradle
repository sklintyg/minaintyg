plugins {
    id "se.inera.intyg.plugin.common" version "2.0.6" apply false
    id "org.gretty" version "2.3.1" apply false
    id "com.moowork.node" version "1.3.1" apply false
}

ext {
    commonVersion = System.properties['commonVersion'] ?: '0-SNAPSHOT'
    infraVersion = System.properties['infraVersion'] ?: '0-SNAPSHOT'
    refDataVersion = System.properties['refDataVersion'] ?: '1.0-SNAPSHOT'

    schemasContractVersion = "2.1.8"

    intygClinicalprocessSchemasVersion = "1.0.22"
    rivtaItintegrationMonitoringSchemasVersion = "1.0.0.5"

    apacheJstlVersion = "9.3.6.v20151106"
    aspectJWeaverVersion = "1.7.1"
    esapiVersion = "2.0.1"
    jaxrsVersion = "1.5.4"
    jsonSchemaValidatorVersion = "2.8.0"
    openWsVersion = "1.4.2-1"
    restassuredVersion = "2.8.0"
    seleniumVersion = "2.52.0"
    servletApiVersion = "2.5"
    springSecuritySaml2CoreVersion = "1.0.1.RELEASE"
    springSecurityVersion = "4.2.5.RELEASE"
    springSessionVersion = "1.3.3.RELEASE"
    springVersion = "4.3.16.RELEASE"
    xmlToolingVersion = "1.3.2"

    mockitoVersion = "2.16.0"

    nodeJsVersion = "10.16.2"
    npmVersion = "6.10.3"
}

allprojects {
    apply plugin: 'se.inera.intyg.plugin.common'
    apply plugin: 'maven'

    repositories {
        mavenLocal()
        maven {
            url "https://build-inera.nordicmedtest.se/nexus/repository/releases/"
            mavenContent {
                releasesOnly()
            }
        }
        maven {
            url "https://build-inera.nordicmedtest.se/nexus/repository/snapshots/"
            mavenContent {
                snapshotsOnly()
            }
        }
        jcenter()
    }

    dependencies {
        testCompile "se.inera.intyg.refdata:refdata:${refDataVersion}"
        testCompile 'junit:junit:4.+'
        testCompile "org.mockito:mockito-core:${mockitoVersion}"
    }

    group = 'se.inera.intyg.minaintyg'
    version = System.properties['buildVersion'] ?: '0-SNAPSHOT'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    task createVersionPropertyFile(type: VersionPropertyFileTask)

    uploadArchives.repositories.mavenDeployer {
        repository(url: "https://build-inera.nordicmedtest.se/nexus/repository/releases/") {
            authentication(userName: System.properties['nexusUsername'], password: System.properties['nexusPassword'])
        }
    }

    [install.repositories.mavenInstaller, uploadArchives.repositories.mavenDeployer]*.pom*.whenConfigured { pom ->
        pom.dependencies.findAll {dep -> dep.groupId == 'se.inera.intyg.common'}.each {
            it.version = se.inera.intyg.IntygPluginKt.findResolvedVersion(project, 'se.inera.intyg.common')
        }
        pom.dependencies.findAll {dep -> dep.groupId == 'se.inera.intyg.infra'}.each {
            it.version = se.inera.intyg.IntygPluginKt.findResolvedVersion(project, 'se.inera.intyg.infra')
        }
    }
}

task tagRelease(type: TagReleaseTask)
