plugins {
    id "se.inera.intyg.plugin.common" version "3.2.9-SNAPSHOT" apply false
    id "org.gretty" version "3.1.1" apply false
    id "nebula.node" version "3.0.0" apply false
    id "io.spring.dependency-management" version "1.1.0" apply false
}

ext {
    commonVersion = System.properties['commonVersion'] ?: '3.19.1-SNAPSHOT'
    infraVersion = System.properties['infraVersion'] ?: '3.19.1-SNAPSHOT'
    refDataVersion = System.properties['refDataVersion'] ?: '1.0-SNAPSHOT'

    schemasContractVersion = "2.1.8"
    intygClinicalprocessSchemasVersion = "1.0.24"
    rivtaItintegrationMonitoringSchemasVersion = "1.0.0.5"

    cxfVersion = "3.6.1"
    esapiVersion = "2.2.0.0"
    swaggerJaxrsVersion = "1.6.0"
    openWsVersion = "1.5.4"
    shedlockSpringVersion = "4.42.0"
    springSecuritySaml2CoreVersion = "1.0.3.RELEASE"
    xmlToolingVersion = "1.4.4"

    nodeJsVersion = "12.22.12"
    npmVersion = "6.14.16"

    springbootMavenBomVersion = "2.7.13"
}

allprojects {
    apply plugin: 'se.inera.intyg.plugin.common'
    apply plugin: 'maven-publish'
    apply plugin: "io.spring.dependency-management"
    apply plugin: 'java-library'

    repositories {
        mavenLocal()
        maven { url "https://nexus.drift.inera.se/repository/it-public/" }
        mavenCentral {
            content {
                // this repository contains everything BUT artifacts with group starting with "se.inera"
                excludeGroupByRegex "se\\.inera.*"
            }
        }
    }

    dependencyManagement {
        imports { mavenBom "org.springframework.boot:spring-boot-dependencies:${springbootMavenBomVersion}" }
    }

    dependencies {
        testImplementation "se.inera.intyg.refdata:refdata:${refDataVersion}"
        testImplementation 'junit:junit'
        testImplementation "org.mockito:mockito-core"
    }

    group = 'se.inera.intyg.minaintyg'
    version = System.properties['buildVersion'] ?: '0-SNAPSHOT'

    sourceCompatibility = 11
    targetCompatibility = 11

    task createVersionPropertyFile(type: VersionPropertyFileTask)

    /*uploadArchives.repositories.mavenDeployer {
        repository(url: "https://nexus.drift.inera.se/repository/maven-releases/") {
            authentication(userName: System.properties['ineraNexusUsername'], password: System.properties['ineraNexusPassword'])
        }
    }

    [install.repositories.mavenInstaller, uploadArchives.repositories.mavenDeployer]*.pom*.whenConfigured { pom ->
        pom.dependencies.findAll { dep -> dep.groupId == 'se.inera.intyg.common' }.each {
            it.version = se.inera.intyg.IntygPluginKt.findResolvedVersion(project, 'se.inera.intyg.common')
        }
        pom.dependencies.findAll { dep -> dep.groupId == 'se.inera.intyg.infra' }.each {
            it.version = se.inera.intyg.IntygPluginKt.findResolvedVersion(project, 'se.inera.intyg.infra')
        }
    }*/
}

gradle.taskGraph.whenReady {
    def runningRestAssuredTest = false
    project('minaintyg-web') { runningRestAssuredTest = gradle.taskGraph.getAllTasks().contains(restAssuredTest) }

    if (runningRestAssuredTest) {
        project('minaintyg-web') { npmInstall.enabled(false); jsTests.enabled(false); test.enabled(false) }
    }
}

clean {
    setDelete(fileTree(buildDir) {
        exclude("/tmp/.cache/expanded/expanded.lock")
    })
}

task tagRelease(type: TagReleaseTask)
