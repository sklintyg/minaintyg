plugins {
    id 'org.springframework.boot' version '3.1.2'
    id 'io.spring.dependency-management' version '1.1.2'
    id 'org.sonarqube' version '4.4.1.3373'
    id 'org.owasp.dependencycheck' version '8.4.0'
}

ext {
    springbootMavenBomVersion = '3.1.2'
    shedlockSpringVersion = '5.10.0'
}

group = 'se.inera.intyg.minaintyg'
version = '4.0.0-SNAPSHOT'

allprojects {
    repositories {
        mavenCentral()
        maven { url "https://build.shibboleth.net/nexus/content/repositories/releases/" }
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'jacoco'
    apply plugin: 'org.sonarqube'
    apply plugin: 'org.owasp.dependencycheck'

    sourceCompatibility = '17'
    targetCompatibility = '17'

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    apply plugin: 'io.spring.dependency-management'

    dependencyManagement {
        imports {
            mavenBom "org.springframework.boot:spring-boot-dependencies:${springbootMavenBomVersion}"
        }
    }

    jacocoTestReport {
        dependsOn test

        reports {
            xml.required = true
            html.outputLocation = layout.buildDirectory.dir('jacocoHtml')
        }

        afterEvaluate {
            classDirectories.setFrom(files(classDirectories.files.collect {
                fileTree(dir: it, exclude: [
                        "**/testability/*"
                ])
            }))
        }
    }

    dependencyCheck {
        formats = List.of("HTML", "JSON")
    }

    sonar {
        properties {
            property "sonar.projectKey", "intyg-minaintyg"
            property "sonar.exclusions", "**/integration/api/**/*,**/testability/*,**/dto/**/*"
            property "sonar.dependencyCheck.htmlReportPath", "${buildDir}/reports/dependency-check-report.html"
        }
    }
}